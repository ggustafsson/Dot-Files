#!/usr/bin/env zsh

# Written by GÃ¶ran Gustafsson <gustafsson.g@gmail.com>.

# Description: This script converts FLAC files to high quality MP3 files.

# Usage: flaccon <file>
#        flaccon *.flac
#        find . -type f -iname "*.flac" -exec flaccon "{}" \;

if [[ $# -gt 0 ]]; then
  echo "Converting FLAC file(s) to MP3."

  for FILE in $*; do
    if [[ -f $FILE ]]; then
      if [[ $FILE == *.flac ]]; then
        TAGS=(TITLE ARTIST ALBUM TRACKNUMBER TOTALTRACKS GENRE DATE)

        # Put all tag information inside of various shell variables.
        for X in $TAGS; do
          TMP=$(metaflac --show-tag=$X $FILE)
          typeset $X=${TMP#*=} # Voodoo magic. Must use typeset because of $.
        done

        echo

        # Print out all tag information.
        for X in $TAGS; do
          echo $X=${(P)X}
        done

        # Decode flac file with "flac" and encode it to mp3 with "lame".
        flac -dc $FILE | lame -b 320 -h --ignore-tag-errors --tt "$TITLE" \
          --ta "$ARTIST" \
          --tl "$ALBUM" \
          --tn "$TRACKNUMBER/$TOTALTRACKS" \
          --tg "$GENRE" \
          --ty "$DATE" \
          --add-id3v2 \
          - "$FILE:r.mp3"
      else
        echo "\nFile $FILE does not end with flac."
      fi
    else
      echo "\nFile $FILE does not exist."
    fi
  done
else
  echo "Usage: $0:t [FILENAME(S)]..."
fi
