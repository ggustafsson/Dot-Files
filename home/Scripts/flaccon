#!/usr/bin/env zsh

# Written by GÃ¶ran Gustafsson <gustafsson.g@gmail.com>.

# Usage: flaccon <file>
#        flaccon *.flac
#        find . -type f -iname "*.flac" -exec flaccon "{}" \;

if [[ $# == 0 ]]; then
  echo "Usage: $0:t [FILENAME(S)]..."
  return
fi

echo "Converting FLAC file(s) to MP3!"

for FILE in $*; do
  if [ -f $FILE ]; then
    if [[ $FILE == *.flac ]]; then
      TAGS=(TITLE ARTIST ALBUM TRACKNUMBER TOTALTRACKS GENRE DATE)

      # Set all tag information as shell variables.
      for X in $TAGS; do
        TMP=$(metaflac --show-tag=$X $FILE)
        typeset -g $X=${TMP#*=}
      done

      echo

      # Print out all tag information.
      for X in $TAGS; do
        echo $X=${(P)X}
      done

      # Decode flac file with "flac" and encode it to mp3 with "lame".
      flac -dc $FILE | lame -b 320 -h --ignore-tag-errors --tt "$TITLE" \
        --ta "$ARTIST" \
        --tl "$ALBUM" \
        --tn "$TRACKNUMBER/$TOTALTRACKS" \
        --tg "$GENRE" \
        --ty "$DATE" \
        --add-id3v2 \
        - "$FILE:r.mp3"
    else
      echo "\nFile $FILE does not end with flac."
    fi
  else
    echo "\nFile $FILE does not exist."
  fi
done

