# GÃ¶ran Gustafsson (gustafsson.g@gmail.com)

export LESS=R
export PAGER=less
export EDITOR=vim
export VISUAL=$EDITOR

HISTFILE=~/.zsh_histfile
HISTSIZE=2000
SAVEHIST=6000

setopt interactivecomments
setopt longlistjobs
setopt nobeep
setopt noclobber
setopt notify
setopt promptsubst

setopt appendhistory
setopt extendedhistory
setopt histexpiredupsfirst
setopt histignoredups
setopt histreduceblanks
setopt histverify
setopt incappendhistory

setopt combiningchars
setopt noautomenu

[[ ! $TERM == dumb ]] && autoload -U colors && colors
autoload -U compinit && compinit
autoload -U url-quote-magic && zle -N self-insert url-quote-magic

bindkey -v
bindkey '^?' backward-delete-char
bindkey -M vicmd 'R' custom-vi-replace

zstyle ':completion:*'          matcher-list 'm:{a-z}={A-Z}'
zstyle ':completion:*'          special-dirs true
zstyle ':completion:*:cd:*'     ignore-parents parent pwd
zstyle ':completion:*:warnings' format "zsh: no matches found."

compdef _path_files cd

precmd() {
	echo
}

function custom-vi-replace {
	REPLACE=1 && zle vi-replace && REPLACE=0
}
zle -N custom-vi-replace

function zle-line-init zle-keymap-select {
	zle reset-prompt
}
zle -N zle-line-init && zle -N zle-keymap-select

function zsh_mode {
	if [[ $KEYMAP == vicmd ]]; then
		echo "%F{red}E%f"
	elif [[ $REPLACE == 1 ]]; then
		echo "%F{magenta}R%f"
	else
		echo "%F{blue}$%f"
	fi
}

if [[ $OSTYPE == freebsd* ]]; then
	PROMPT='%B${(C)USER} %F{red}${(C)HOST%%.*}%f%b %~ %B$(zsh_mode)%b '
else
	PROMPT='%B${(C)USER} %F{blue}${(C)HOST%%.*}%f%b %~ %B$(zsh_mode)%b '
fi

