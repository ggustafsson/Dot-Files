#!/usr/bin/env zsh

# Written by GÃ¶ran Gustafsson <gustafsson.g@gmail.com>.

# Description: This script converts FLAC files to high quality MP3 files.

# Usage: flaccon <file>
#        flaccon *.flac
#        find . -type f -iname "*.flac" -exec flaccon "{}" \;

if [[ ! $# -gt 0 ]]; then
  echo "Usage: $0:t [FILENAME(S)]..."
  exit
fi

echo "Converting FLAC file(s) to MP3 file(s)."

for file in $*; do
  echo

  if [[ ! -f $file ]]; then
    echo "File '$file' does not exist!"
    continue
  fi

  if [[ ! $file == *.flac ]]; then
    echo "File '$file' does not end with flac."
    continue
  fi

  # All the tags that will be saved.
  tags=(title artist album tracknumber totaltracks genre date)

  # Put all tag information inside of various shell variables.
  for tag in $tags; do
    info=$(metaflac --show-tag=$tag $file)
    typeset $tag=${info#*=} # Set values for $title, $artist and so on.
  done

  print -P "%B%F{cyan}Converting file '$file' to MP3.%f%b\n"

  # Print out all tag information.
  for tag in $tags; do
    echo "${(U)tag}=${(P)tag}"
  done

  # Decode FLAC file with "flac" and encode it to MP3 with "lame".
  flac -dc $file | lame -b 320 -h --ignore-tag-errors \
    --tt "$title" \
    --ta "$artist" \
    --tl "$album" \
    --tn "$tracknumber/$totaltracks" \
    --tg "$genre" \
    --ty "$date" \
    --add-id3v2 - "$file:r.mp3"
done
