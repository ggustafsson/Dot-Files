#!/usr/bin/env zsh

# Written by GÃ¶ran Gustafsson <gustafsson.g@gmail.com>.

# Description: This script checks the git status of all my projects.

# Usage: chkp

for DIRECTORY in ~/Projects/*; do
  cd $DIRECTORY

  if [[ -n $(git rev-list origin..HEAD 2> /dev/null) ]]; then
    print -P "%B%F{cyan}$DIRECTORY:t:%f%b"
    git status --branch --short
    echo
  elif [[ -n $(git status --short 2> /dev/null) ]]; then
    print -P "%B%F{red}$DIRECTORY:t:%f%b"
    git status --branch --short
    echo
  elif [[ -d .git ]]; then
    CLEAN_REPO+=($DIRECTORY:t)
  else
    NOT_REPO+=($DIRECTORY:t)
  fi
done

if [[ ${#CLEAN_REPO} -gt 0 ]]; then
  print -P "%B%F{green}Clean repo:%f%b"
  for X in $CLEAN_REPO; do
    echo " * $X"
  done
fi

[[ ${#CLEAN_REPO} -gt 0 && ${#NOT_REPO} -gt 0 ]] && echo

if [[ ${#NOT_REPO} -gt 0 ]]; then
  print -P "%B%F{blue}Not Git repo:%f%b"
  for X in $NOT_REPO; do
    echo " * $X"
  done
fi
