#!/usr/bin/env zsh

# Written by GÃ¶ran Gustafsson <gustafsson.g@gmail.com>.

# Description: This script checks the git status of all my projects.

# Usage: chkp

echo "Checking status of projects."

for directory in ~/Projects/*; do
  cd $directory

  if [[ -n $(git rev-list origin..HEAD 2> /dev/null) ]]; then
    print -P "\n%B%F{cyan}$directory:t:%f%b"
  elif [[ -n $(git status --short 2> /dev/null) ]]; then
    print -P "\n%B%F{red}$directory:t:%f%b"
  elif [[ -d .git ]]; then
    clean_repos+=($directory:t)
    continue
  else
    not_repos+=($directory:t)
    continue
  fi

  git status --branch --short
done

if [[ ${#clean_repos} -gt 0 ]]; then
  echo
  print -P "%B%F{green}Clean repo(s):%f%b"
  for repo_name in $clean_repos; do
    echo " * $repo_name"
  done
fi

if [[ ${#not_repos} -gt 0 ]]; then
  echo
  print -P "%B%F{blue}Not Git repo(s):%f%b"
  for dir_name in $not_repos; do
    echo " * $dir_name"
  done
fi
