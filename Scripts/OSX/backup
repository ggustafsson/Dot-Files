#!/usr/bin/env zsh

# Original script created by Zach Holman:
#   <https://github.com/holman/dotfiles/blob/master/bin/backup>.
# Modified by GÃ¶ran Gustafsson <gustafsson.g@gmail.com>.

# Description: This script mounts an encrypted dmg file (sparseimage), backs
# up a few specific directories to it and then unmounts the image file.

# Usage: backup

device=Keychain
sparseimage=/Volumes/$device/Backup.sparseimage
destination=/Volumes/Backup

directories=(
  ~/Development
  ~/Documents
  ~/Dropbox
  ~/Library/Application\ Support/Alfred\ 2/Alfred.alfredpreferences
  ~/Library/Application\ Support/OpenEmu
  ~/Library/Safari/Bookmarks.plist
  ~/Movies
  ~/Music/FLAC
  ~/Music/GBS
  ~/Music/MP3
  ~/Music/NSFe
  ~/Music/Ringtones
  ~/Pictures
  ~/Projects
  ~/School
  ~/Sites
  ~/Storage
  ~/Web\ Archives
  ~/YouTube
)

if [[ ! -f $sparseimage ]]; then
  echo "File '$sparseimage' does not exist! Exiting..."
  exit 1
fi

echo "Mounting encrypted image file."
hdiutil attach -noverify $sparseimage

if [[ ! -d $destination ]]; then
  echo "\nDirectory '$destination' does not exist! Exiting..."
  exit 1
fi

for directory in $directories; do
  if [[ -d $directory || -f $directory ]]; then
    print -P "\n%B%F{green}Backing up directory ${directory/$HOME/~}.%f%b"
    rsync --archive --delete --exclude=Dropbox/.dropbox.cache --human-readable --verbose $directory $destination
  else
    echo "\nDirectory '${directory/$HOME/~}' not found! Exiting..."
    exit 1
  fi
done

echo "\nUnmounting encrypted image file and device."
hdiutil detach $destination && diskutil eject $device
