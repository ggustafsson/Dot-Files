#!/usr/bin/env zsh

# Description: This script mounts an encrypted dmg file (sparseimage), backs up
# a few specific directories/files to it and then unmounts the image file.
#
# Usage: backup
#
# Original script created by Zach Holman:
#   <https://github.com/holman/dotfiles/blob/master/bin/backup>
# Modified by GÃ¶ran Gustafsson (gustafsson.g at gmail.com).

device=Keychain
sparseimage=/Volumes/${device}/Backup.sparseimage
destination=/Volumes/Backup

backup_objects=(
  ~/.ssh
  ~/Development
  ~/Documents
  ~/Dropbox
  ~/Golang
  ~/Library/Application\ Support/OpenEmu
  ~/Library/Containers/com.apple.iWork.Pages/Data/Library/Application\ Support/User\ Templates
  ~/Library/Safari/Bookmarks.plist
  ~/Movies
  ~/Music/FLAC
  ~/Music/GBS
  ~/Music/MP3
  ~/Music/NSFe
  ~/Pictures
  ~/Projects
  ~/School
  ~/Sites
  ~/Storage
  ~/Web\ Archives
)

if [[ ! -f $sparseimage ]]; then
  echo "File '${sparseimage}' does not exist! Exiting..."
  exit 1
fi

echo "Mounting encrypted image file."
hdiutil attach -noverify $sparseimage

if [[ ! -d $destination ]]; then
  echo "\nDirectory '${destination}' does not exist! Exiting..."
  exit 1
fi

for object in $backup_objects; do
  if [[ -a $object ]]; then
    print -P "\n%B%F{green}Backing up directory ${object/$HOME/~}.%f%b"
    rsync --archive --delete --exclude=Dropbox/.dropbox.cache \
      --exclude=Dropbox/Public/Tmp --exclude=Dropbox/Shared\ Files \
      --human-readable --verbose \
      $object $destination
  else
    echo "\nDirectory '${object/$HOME/~}' not found! Exiting..."
    exit 1
  fi
done

echo "\nUnmounting encrypted image file and device."
hdiutil detach $destination && diskutil eject $device
