#!/usr/bin/env zsh

# Description: This script backs up several directories/files to two servers
# with rsync (one through ssh and one locally mounted).
#
# Usage: backup2s
#
# Written by GÃ¶ran Gustafsson (gustafsson.g at gmail.com).
# License: BSD 3-Clause.

server1=hoth
server2=tatooine

destination1=/media/external
destination2=/Volumes/External

backup_server1=(
  /Volumes/External/Disk\ Images
  /Volumes/External/Documentaries
  /Volumes/External/Movies
  /Volumes/External/Series
  ~/Movies/YouTube
  ~/Music/FLAC
  ~/Music/MP3
)

backup_server2=(
  ~/Music/FLAC
  ~/Music/MP3
)

if [[ ! -d $destination2 ]]; then
  echo "Directory '${destination2}' does not exist! Exiting..."
  exit 1
fi

echo "Backing up to server '${server1}'."

for object in $backup_server1; do
  if [[ -a $object ]]; then
    print -P "\n%B%F{green}Backing up: ${object/${HOME}/~}%f%b"
    rsync --archive --chmod=D755,F644 --delete --human-readable --progress \
      $object -e ssh ${server1}:$destination1
  else
    echo "\nObject '${object}' not found! Exiting..."
    exit 1
  fi
done

echo "\nBacking up to server '${server2}'."

for object in $backup_server2; do
  if [[ -a $object ]]; then
    print -P "\n%B%F{green}Backing up: ${object/${HOME}/~}%f%b"
    rsync --archive --chmod=D755,F644 --delete --human-readable --progress \
      $object $destination2
  else
    echo "\nObject '${object}' not found! Exiting..."
    exit 1
  fi
done
